# (c) Silvaco Inc., 2015
go athena
#
line x loc=0 spac=0.1 
line x loc=0.04 spac=0.006
line x loc=0.08 spac=0.006
line x loc=0.1 spac=0.01 
#
line y loc=0.00 spac=0.002 
line y loc=0.2 spac=0.005
line y loc=0.5 spac=0.05
line y loc=0.8 spac=0.15 
#
init orientation=100 c.phos=1e14 space.mul=2
#
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
implant boron dose=8e12 energy=100 pearson
#
diffus temp=950 time=11 weto2 hcl=3
#
diffus time=0 temp=1000 t.rate=4.000 dryo2 press=0.10 hcl=3
#
diffus time=0 temp=1200 nitro press=1
#
diffus time=0 temp=1200 t.rate=-4.444 nitro press=1
#
etch oxide all
#
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
diffus time=11 temp=925 dryo2 press=1.00 hcl=3
#
extract name="gateox" thickness oxide mat.occno=1 x.val=0.50
#
implant boron dose=9.5e11 energy=10 pearson
#
depo poly thick=0.2 divi=10 
#
etch poly left p1.x=0.065
#
method fermi compress
diffuse time=3 temp=900 weto2 press=1.0
#
implant phosphorus dose=3.0e13 energy=11 pearson
#
depo oxide thick=0.020 divisions=8
#
etch oxide dry thick=0.020
#
implant arsenic dose=5.0e14 energy=20 pearson
#
method fermi compress
diffuse time=1 temp=900 nitro press=1.0
#
etch oxide left p1.x=0.065
deposit aluminum thick=0.03 divi=2
etch aluminum right p1.x=0.036
#
extract name="nxj" xj silicon mat.occno=1 x.val=0.1 junc.occno=1
extract name="n1dvt" 1dvt ntype vb=0.0 qss=1e10 x.val=0.49
extract start material="Polysilicon" mat.occno=1 bias=0.0 bias.step=0.2 bias.stop=2 x.val=0.45
extract done name="sheet cond v bias" curve(bias,1dn.conduct material="Silicon" mat.occno=1 region.occno=1) outfile="extract.dat"
extract name="n++ sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.05 region.occno=1
extract name="ldd sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.3 region.occno=1
extract name="chan surf conc" surf.conc impurity="Net Doping" material="Silicon" mat.occno=1 x.val=0.45
#
structure mirror right
#
electrode name=gate x=0.1 y=0.1
electrode name=source x=0.02
electrode name=drain x=0.18
electrode name=substrate backside

structure outfile=mos65nm.str

# plot the structure
tonyplot -st mos65nm.str -set mos65nm.set


go atlas

# define the Gate workfunction 
contact name=gate n.poly

# Define the Gate Qss
interface qf=3e10

# Use the cvt mobility model for MOS 
models cvt srh print numcarr=2 
method climit=1e-4 maxtrap=10
# set gate biases with Vds=0.0 
solve init
solve vgate=1.1 outf=solve_tmp1_65nm 
solve vgate=2.2 outf=solve_tmp2_65nm
solve vgate=3.3 outf=solve_tmp3_65nm

#load in temporary files and ramp Vds
load infile=solve_tmp1_65nm 
log outf=mos65nm_1.log
solve name=drain vdrain=0 vfinal=3.3 vstep=0.3 
 
load infile=solve_tmp2_65nm
log outf=mos65nm_2.log
solve name=drain vdrain=0 vfinal=3.3 vstep=0.3
  
load infile=solve_tmp3_65nm
log outf=mos65nm_3.log
solve name=drain vdrain=0 vfinal=3.3 vstep=0.3

tonyplot -overlay -st mos65nm_1.log  mos65nm_2.log mos65nm_3.log -set mos65nm_1.set

solve init

# Bias the drain 
solve vdrain=0.1 

# Ramp the gate
log outf=mos65nm_4.log master
solve vgate=0 vstep=0.25 vfinal=3.0 name=gate
save outf=mos65nm_4.str

# plot results
tonyplot  mos65nm_4.log -set mos65nm_4.set

quit


